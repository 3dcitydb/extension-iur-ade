plugins {
    id 'java'
    id 'distribution'
}

group 'org.citydb.ade'
version '1.4.0'
description 'i-Urban Revitalization ADE extension for the 3DCityDB'

java {
    sourceCompatibility = JavaVersion.VERSION_11
}

configurations {
    citygml4j
}

repositories {
    maven {
        url 'https://dl.bintray.com/3dcitydb/maven'
    }
    maven {
        url 'https://repo.osgeo.org/repository/release'
    }
    jcenter()
}

dependencies {
    implementation 'org.citydb:impexp-client:4.3.0-rc1'
    implementation 'org.citygml4j.ade:iur-ade-citygml4j:1.4.0'

    citygml4j('org.citygml4j.ade:iur-ade-citygml4j:1.4.0') {
        transitive = false
    }
}

jar {
    manifest {
        attributes('Implementation-Title': project.name,
                'Implementation-Version': project.version
        )
    }
    into('META-INF') {
        from 'LICENSE'
    }
}

distributions.main {
    distributionBaseName  = 'iur-ade'
    contents {
        from 'README.md'
        into('lib') {
            from jar
            from configurations.citygml4j
        }
        into('schema-mapping') {
            from 'resources/database/schema-mapping'
        }
        into('3dcitydb') {
            from 'resources/database/3dcitydb'
        }
    }
}